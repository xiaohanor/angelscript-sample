
/**
 * Access specifiers give more granular control over which
 * other classes can use specific functions and variables.
 * 
 * This allows a component to only allow certain capabilities
 * to call functions on it, for example.
 */
class UAccessSpecifierExample
{
	// Access specifiers need to be declared in the class they are used in
	// All access specifiers start with private or protected as a base.
	access Internal = private;

	// This would be equivalent to `private`
	access:Internal
	float PrivateFloatValue = 0.0;



	// From there, you can add a list of classes that should also be granted access
	// on top of the private or protected access.
	access InternalWithCapability = private, UAccessSpecifierCapability, AHazePlayerCharacter;

	// This can be accessed as private, or by etiher of the two classes specified above
	access:InternalWithCapability
	float AccessibleFloatValue = 1.0;

	// Functions can also use access specifiers
	access:InternalWithCapability
	void AccessibleMethod()
	{
	}


	// It is also possible to restrict the type of access that a particular class
	// gets, using modifiers. Available modifiers are:
	//
	// editdefaults:
	//	The class can only set the property or call the function from a `default` statement or a ConstructionScript.
	// 
	// readonly:
	//  The class can only read properties or call const methods, not do anything that can modify it.
	//
	access SpecifierCapabilityCanOnlyRead = private, UAccessSpecifierCapability (readonly);

	// This can be read from UAccessSpecifierCapability, but not changed
	access:SpecifierCapabilityCanOnlyRead
	float CapabilityReadOnlyValue = 0.0;



	// Normally, when specifying a class, only that specific class has access, not its children.
	// With the `inherited` modifier, you can give all child classes of the specified class the same access.
	access ReadableInAnyCapability = private, UHazeCapability (inherited, readonly);

	// This value is private, but can be read (not written) from capabilities only
	access:ReadableInAnyCapability
	float CanBeReadByCapabilities = 10.0;



	// Instead of writing a class name you can specify just `*` to give access to all classes.
	// This should be used in combination with modifiers.
	access EditAndReadOnly = private, * (editdefaults, readonly);

	// This value is read-only outside this class, but can be edited from `default` statements by any class
	UPROPERTY(EditDefaultsOnly, BlueprintReadOnly, Category = "Options")
	access:EditAndReadOnly
	bool bOptionOnlyEditable = false;



	// Global functions can also be given access just like classes
	//  Note that neither classes nor functions need to be imported to be given access
	access RestrictedToSpecificGlobalFunction = private, ExampleCallRestrictedFunction;

	access:RestrictedToSpecificGlobalFunction
	void RestrictedFunction()
	{
	}
};

void ExampleCallRestrictedFunction()
{
	UAccessSpecifierExample Example;
	Example.RestrictedFunction();
}

class UAccessSpecifierCapability : UHazeCapability
{
};